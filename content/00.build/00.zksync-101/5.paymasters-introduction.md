---
title: Paymasters introduction
description: Learn about paymasters and use one to pay transaction fees with your own token
---

This tutorial makes use of smart contracts deployed in the previous two tutorials, [Deploy your first
contract](quickstart) and [Create an ERC20 token](erc20-token). This section introduces one of the custom features
of zkSync: native account abstraction and paymasters.

In this tutorial we will:

:check-icon Learn about paymasters.

:check-icon Review the testnet paymaster smart contract code.

:check-icon Use the testnet paymaster to pay transaction fees with our own ERC20 token.

Before you start, make sure that you’ve configured the zkSync Sepolia tesnet in your browser wallet by [following the2.connect-zksync.md2.connect-zksync.md
instructions here](2.connect-zksync.md).

## What is a paymaster?

Paymasters are smart contracts with a very specific function: pay the transaction fees on behalf of the user. In order
to do so, paymasters  have ETH balance which they use to pay the transaction fees to the protocol.

Being smart contracts, paymasters can have logic and validations to pay, or not the transaction fees.

Every paymaster has these two functions:

- `validateAndPayForPaymasterTransaction` : it receives the transaction parameters (fields like `from`, `amount` , `to`
  ), use these to do the required validations, and pay the transaction fee.
- `postTransaction`: this function runs after the transaction is executed and it’s optional.

![zksync paymaster](/images/101-paymasters/zksync-paymaster.png)

## Paymaster smart contract code

Although application developers are encouraged to create their own paymaster smart contract, zkSync provides a testnet
paymaster for convenience and testing purposes.

::callout{icon="i-heroicons-light-bulb"}
The testnet paymaster address is
[0x3cb2b87d10ac01736a65688f3e0fb1b070b3eea3](https://sepolia.explorer.zksync.io/address/0x3cb2b87d10ac01736a65688f3e0fb1b070b3eea3)
::

Here is the code:

::drop-panel
  ::panel{label="TestnetPaymaster.sol"}
        ```solidity
        // SPDX-License-Identifier: MIT

        pragma solidity 0.8.20;

        import "@openzeppelin/contracts/token/ERC20/IERC20.sol";

        import "./interfaces/IPaymaster.sol";
        import "./interfaces/IPaymasterFlow.sol";
        import "./L2ContractHelper.sol";

        // This is a dummy paymaster. It expects the paymasterInput to contain its "signature" as well as the needed exchange rate.
        // It supports only approval-based paymaster flow.
        contract TestnetPaymaster is IPaymaster {
            function validateAndPayForPaymasterTransaction(
                bytes32,
                bytes32,
                Transaction calldata _transaction
            ) external payable returns (bytes4 magic, bytes memory context) {
                // By default we consider the transaction as accepted.
                magic = PAYMASTER_VALIDATION_SUCCESS_MAGIC;

                require(msg.sender == BOOTLOADER_ADDRESS, "Only bootloader can call this contract");
                require(_transaction.paymasterInput.length >= 4, "The standard paymaster input must be at least 4 bytes long");

                bytes4 paymasterInputSelector = bytes4(_transaction.paymasterInput[0:4]);
                if (paymasterInputSelector == IPaymasterFlow.approvalBased.selector) {
                    // While the actual data consists of address, uint256 and bytes data,
                    // the data is not needed for the testnet paymaster
                    (address token, uint256 amount, ) = abi.decode(_transaction.paymasterInput[4:], (address, uint256, bytes));

                    // Firstly, we verify that the user has provided enough allowance
                    address userAddress = address(uint160(_transaction.from));
                    address thisAddress = address(this);

                    uint256 providedAllowance = IERC20(token).allowance(userAddress, thisAddress);
                    require(providedAllowance >= amount, "The user did not provide enough allowance");

                    // The testnet paymaster exchanges X wei of the token to the X wei of ETH.
                    uint256 requiredETH = _transaction.gasLimit * _transaction.maxFeePerGas;
                    if (amount < requiredETH) {
                        // Important note: while this clause definitely means that the user
                        // has underpaid the paymaster and the transaction should not accepted,
                        // we do not want the transaction to revert, because for fee estimation
                        // we allow users to provide smaller amount of funds then necessary to preserve
                        // the property that if using X gas the transaction success, then it will succeed with X+1 gas.
                        magic = bytes4(0);
                    }

                    // Pulling all the tokens from the user
                    try IERC20(token).transferFrom(userAddress, thisAddress, amount) {} catch (bytes memory revertReason) {
                        // If the revert reason is empty or represented by just a function selector,
                        // we replace the error with a more user-friendly message
                        if (revertReason.length <= 4) {
                            revert("Failed to transferFrom from users' account");
                        } else {
                            assembly {
                                revert(add(0x20, revertReason), mload(revertReason))
                            }
                        }
                    }

                    // The bootloader never returns any data, so it can safely be ignored here.
                    (bool success, ) = payable(BOOTLOADER_ADDRESS).call{value: requiredETH}("");
                    require(success, "Failed to transfer funds to the bootloader");
                } else {
                    revert("Unsupported paymaster flow");
                }
            }

            function postTransaction(
                bytes calldata _context,
                Transaction calldata _transaction,
                bytes32,
                bytes32,
                ExecutionResult _txResult,
                uint256 _maxRefundedGas
            ) external payable override {
                // Nothing to do
            }

            receive() external payable {}
        }
    ```
  ::
::

In the `validateAndPayForPaymasterTransaction` it is:

1. checking that the paymasterInput is `approvalBased`.
2. checks that the allowance of a given ERC20 is enough.
3. Transfers the transaction fee (`requiredETH`) in ERC20 from the user’s balance to the paymaster.
4. Transfer the transaction fee in ETH from the paymaster contract to the bootloader.

## How to send a transaction through a paymaster?

In order to send a transaction through a paymaster, the transaction must include the following additional parameters:

- `paymasterAddress`: the smart contract address of the paymaster
- `type`: should be `General` or `ApprovalBased` (to pay fees with ERC20 tokens)
- `minimalAllowance`: the amount of ERC20 tokens to be approved for spending (for `approvalBased` type paymasters only).
- `innerInput`: any payload we want to send to the paymaster (optional).

We’ll see an example next.

## Interacting with the testnet paymaster

We’re going to interact with the `ZeekSecretMessages.sol` contract that we created in the first tutorial and use the
ERC20 token that we deployed in the second tutorial to pay the transaction fees.

::content-switcher
---
items: [{
  label: 'Atlas',
  partial: '_paymaster_intro/_atlas_paymaster_intro'
}, {
  label: 'Remix',
  partial: '_paymaster_intro/_remix_paymaster_intro'
}]
---
::

## Takeaways

- Paymasters on zkSync allow any account to pay fees with ERC20 tokens or enable gasless transactions.
- Paymasters are smart contracts that can have any validations and rules.
- To send a transaction through a paymaster, we only need to include additional parameters in the transaction.

## Next steps

- Learn more about paymasters and native account abstraction in this section of the docs.
- Browse different paymaster examples in [this open source
  repository](https://github.com/matter-labs/paymaster-examples).
- Continue learning by building a GoFundMe clone on zkSync.
